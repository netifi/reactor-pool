plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'io.projectreactor'
version = '0.0.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    api 'io.projectreactor:reactor-core:3.2.3.RELEASE'
    api 'io.projectreactor.addons:reactor-extra:3.2.0.RELEASE'
    api 'org.slf4j:slf4j-api:1.7.21'
}

repositories {
    jcenter()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://artifactory.netifiinc.com/artifactory/libs-snapshot-local' : 'https://artifactory.netifiinc.com/artifactory/libs-release-local'
            credentials {
                username = project.findProperty('netifiArtifactoryUsername') ?: System.getenv('NETIFI_ARTIFACTORY_USERNAME')
                password = project.findProperty('netifiArtifactoryPassword') ?: System.getenv('NETIFI_ARTIFACTORY_PASSWORD')
            }
        }
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
